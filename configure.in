dnl $Id$

AC_INIT(lib/libgadu.h)

AC_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_PATH_PROG(GMAKE, gmake, no)
if test "x$GMAKE" = "xno"; then
  AC_PATH_PROG(MAKE, make, no)
  if test "x$MAKE" = "xno"; then
    AC_MSG_ERROR([make ])
  fi
else
  MAKE="$GMAKE"
fi

AC_PATH_PROG(AR, ar, no)
if test "x$AR" = "xno"; then
  AC_MSG_ERROR([ar ])
fi

AC_NEED_STDINT_H
AC_C_BIGENDIAN

AC_SUBST(CFLAGS_LIBGADU)
AC_SUBST(LIBS_LIBGADU)

AC_SUBST(ioctld, "")
AC_SUBST(install_ioctld, "")

AC_SUBST(shared, "")
AC_SUBST(install_shared, "")

AC_SUBST(static, "static")
AC_SUBST(install_static, "install-static")

AC_SUBST(ekg, "")
AC_SUBST(install_ekg, "")
AC_SUBST(clean_ekg, "")
AC_SUBST(distclean_ekg, "")

# najpierw sprawdzamy te opcje, które s± wspólne dla libgadu i ekg.

AC_ARG_WITH(debug, 
  [  --without-debug  	  Compile without debug (-Wall, -ggdb)])

if test "x$with_debug" = "xno"; then 
  CFLAGS="$CFLAGS -DGG_DEBUG_DISABLE"
else
  CFLAGS="$CFLAGS -ggdb" 
fi

AC_CHECK_LIB(nsl, t_accept, LIBS="$LIBS -lnsl")
AC_CHECK_LIB(socket, socket, LIBS="$LIBS -lsocket")

# zachowujemy opcje dla libgadu

CFLAGS_LIBGADU="$CFLAGS"
LIBS_LIBGADU="$LIBS"

# i zaczynamy wykrywaæ ¶mieci dla ekg

if test -d src; then

  AC_ARG_WITH(termcap, 
    [  --without-termcap       Used to force curses/ncurses])

  if test "x$with_termcap" = "xno"; then 
    TERMCAP_LIB=""
  else
    AC_CHECK_LIB(termcap, tgetent, TERMCAP_LIB=-ltermcap)
    LIBS="$LIBS $TERMCAP_LIB"
  fi

  if test "x$TERMCAP_LIB" = "x"; then 
    AC_CHECK_CURSES
    LIBS="$LIBS $CURSES_LIBS"
    CFLAGS="$CFLAGS $CURSES_INCLUDEDIR"
  fi

  AC_CHECK_READLINE

  if test "x$READLINE_LIBS" = "x"; then
    AC_MSG_RESULT([not found])
    AC_MSG_ERROR([You need both readline and readline-devel packages.])
  fi

  LIBS="$READLINE_LIBS $LIBS"
  CFLAGS="$CFLAGS $READLINE_INCLUDES"

  AC_CHECK_FUNCS([rl_set_prompt rl_filename_completion_function rl_get_screen_size rl_set_key])

  AC_ARG_WITH(libgsm,
    [  --without-libgsm        Compile without GSM support])

  if test "x$with_libgsm" != "xno"; then
    AC_CHECK_LIB(gsm, gsm_decode, [
      AC_CHECK_HEADERS([gsm.h linux/soundcard.h])
      LIBS="$LIBS -lgsm"])
  fi
 
  AC_ARG_WITH(ioctld,
    [  --with-ioctld           Compile with ioctld (Linux only)])

  if test "x$with_ioctld" = "xyes"; then 
    CFLAGS="$CFLAGS -DWITH_IOCTLD -DIOCTLD_PATH=\\\"\$(prefix)/bin/ioctld\\\""
    ioctld="ioctld"
    install_ioctld="install-ioctld"
  fi

  AC_ARG_WITH(shared,
    [  --with-shared           Compile shared version of libgadu])

  if test "x$with_shared" = "xyes"; then 
    shared="shared"
    install_shared="install-shared"
  fi
  ekg="ekg"
  install_ekg="install-ekg"
  clean_ekg="clean-ekg"
  distclean_ekg="distclean-ekg"
else
  shared="shared"
  install_shared="install-shared"
  
  AC_ARG_WITH(static,
    [  --without-static        Don't compile static version of libgadu])

  if test "x$with_static" = "xno"; then
    static=""
    install_static=""
  fi
fi

AC_CONFIG_FILES(Makefile lib/Makefile examples/Makefile)
if test -d src; then
  AC_CONFIG_FILES(src/Makefile)
fi

AC_OUTPUT


